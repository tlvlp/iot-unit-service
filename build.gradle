plugins {
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.palantir.docker' version '0.22.1'
	id 'java'
}

group = 'com.tlvlp'
version = '1.0.0'
sourceCompatibility = '11'

ext {
	projectName = 'iot-unit-service'
	dockerRepo = 'tlvlp'
	mainClass = 'com.tlvlp.iot.server.unit.service.Application'
	servicePort = 8200
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation "org.mockito:mockito-core:3.+"
	testImplementation "org.mockito:mockito-junit-jupiter:3.+"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR3"
	}
}

test {
	useJUnitPlatform()
}

docker {
	name "$dockerRepo/$projectName:$project.version"
	tag "$project.version", "$dockerRepo/$projectName:$project.version"
	tag 'latest', "$dockerRepo/$projectName:latest"
	dependsOn bootJar
	files bootJar.outputs
	dockerfile file('Dockerfile')
	buildArgs([PROJECT: projectName, SERVICE_PORT: servicePort.toString()])
	pull true
	noCache true
}

bootJar {
	mainClassName = mainClass
	manifest {
		attributes('Implementation-Title': projectName,
				'Implementation-Version': version)
	}
}